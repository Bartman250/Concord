group 'com.tradeix.concord'
version '1.0'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.springframework.boot'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'application'
apply plugin: 'distribution'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
}

sourceSets {
    main.kotlin.srcDirs = ['src/main/kotlin']
    main.java.srcDirs = []
    main.resources.srcDirs = ['src/main/resources']
    test.kotlin.srcDirs = []
    test.java.srcDirs = []
    test.resources.srcDirs = []
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    cordapp project(":tradeix-concord-shared")
    cordapp project(":tradeix-concord-shared-client")
    cordapp project(":tradeix-concord-cordapp-supplier")
    cordaCompile "$corda_release_group:corda-core:$corda_release_version"
    compile "org.apache.activemq:artemis-commons:$active_mq_version"
    compile "org.apache.activemq:artemis-core-client:$active_mq_version"
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile "$corda_release_group:corda-node-driver:$corda_release_version"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.1"
        apiVersion = "1.1"
        jvmTarget = "1.8"
        javaParameters = true
    }
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { it.getName() }.join(' '),
                'Main-Class': 'com.tradeix.concord.cordapp.supplier.client.receiver.Application'
        )
    }
}

bootRepackage {
    mainClassName = 'com.tradeix.concord.cordapp.supplier.client.receiver.Application'
}

distributions {
    main {
        contents {
            baseName = project.name
            into('bin') {
                from('src/main/resources/receiver.properties')
            }
        }
    }
}