group 'com.tradeix.concord'
version '1.0'

buildscript {
    ext.kotlin_version = '1.1.60'
    ext.corda_release_group = 'net.corda'
    ext.corda_release_version = '3.1-corda'
    ext.corda_gradle_plugins_version = '3.2.1'
    ext.quasar_version = '0.7.9'
    ext.junit_version = '4.12'
    ext.gson_version = '2.8.0'
    ext.spring_boot_version = '1.5.4.RELEASE'
    ext.active_mq_version = "2.2.0"
    ext.jackson_version = "2.9.2"
    ext.guava_version = "21.0"

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'


sourceSets {
    main.kotlin.srcDirs = []
    main.java.srcDirs = []
    main.resources.srcDirs = []
    test.kotlin.srcDirs = []
    test.java.srcDirs = []
    test.resources.srcDirs = []
}

dependencies {
    cordaCompile "$corda_release_group:corda-rpc:$corda_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"
    cordaRuntime "$corda_release_group:corda-webserver:$corda_release_version"

    cordapp project(":tradeix-concord-shared")
    cordapp project(":tradeix-concord-shared-domain")
    cordapp project(":tradeix-concord-shared-cordapp")

    cordapp project(":tradeix-concord-cordapp-supplier")
    cordapp project(":tradeix-concord-cordapp-funder")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.1"
        apiVersion = "1.1"
        jvmTarget = "1.8"
        javaParameters = true
    }
}

def $buyerCorDapp = [
        "$project.group:tradeix-concord-shared:$project.version",
        "$project.group:tradeix-concord-shared-domain:$project.version",
        "$project.group:tradeix-concord-shared-cordapp:$project.version"
]

def $supplierCorDapp = [
        "$project.group:tradeix-concord-shared:$project.version",
        "$project.group:tradeix-concord-shared-domain:$project.version",
        "$project.group:tradeix-concord-shared-cordapp:$project.version",
        "$project.group:tradeix-concord-cordapp-supplier:$project.version"
]

def $funderCorDapp = [
        "$project.group:tradeix-concord-shared:$project.version",
        "$project.group:tradeix-concord-shared-domain:$project.version",
        "$project.group:tradeix-concord-shared-cordapp:$project.version",
        "$project.group:tradeix-concord-cordapp-funder:$project.version"
]

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    directory "./build/nodes"
    node {
        name "O=Notary,L=London,C=GB"
        notary = [validating: true]
        p2pPort 10002
    }
    node {
        name "O=Test Supplier 1,L=London,C=GB"
        p2pPort 10005
        rpcSettings {
            address("localhost:10006")
            adminAddress("localhost:10046")
        }
        webPort 10007
        cordapps = $supplierCorDapp
        rpcUsers = [[user: "guest", "password": "letmein", "permissions": ["ALL"]]]
    }
    node {
        name "O=Test Funder 1,L=New York,C=US"
        p2pPort 10008
        rpcSettings {
            address("localhost:10009")
            adminAddress("localhost:10049")
        }
        webPort 10010
        cordapps = $funderCorDapp
        rpcUsers = [[user: "guest", "password": "letmein", "permissions": ["ALL"]]]
    }
    node {
        name "O=Test Buyer 1,L=Paris,C=FR"
        p2pPort 10011
        rpcSettings {
            address("localhost:10012")
            adminAddress("localhost:10052")
        }
        webPort 10013
        cordapps = $buyerCorDapp
        rpcUsers = [[user: "guest", "password": "letmein", "permissions": ["ALL"]]]
    }
}

task createDemobenchProfile(type: Zip, dependsOn: ['deployNodes']) {
    from ("./build/nodes/Notary") {
        include "/cordapps/**"
        into "notary/"
    }
    from("./config/nodes/") {
        include "notary.node.conf"
        into "notary/"
        rename "notary.node.conf", "node.conf"
    }

    for(item in (1..3)) {
        from ("./build/nodes/Test Funder 1") {
            include "/cordapps/**"
            into "funder $item/"
        }
        from("./config/nodes/") {
            include "funder${item}.node.conf"
            into "funder $item/"
            rename "funder${item}.node.conf", "node.conf"
        }
    }

    for(item in (1..3)) {
        from ("./build/nodes/Test Buyer 1") {
            include "/cordapps/**"
            into "buyer $item/"
        }
        from("./config/nodes/") {
            include "buyer${item}.node.conf"
            into "buyer $item/"
            rename "buyer${item}.node.conf", "node.conf"
        }
    }

    for(item in (1..3)) {
        from ("./build/nodes/Test Supplier 1") {
            include "/cordapps/**"
            into "supplier $item/"
        }
        from("./config/nodes/") {
            include "supplier${item}.node.conf"
            into "supplier $item/"
            rename "supplier${item}.node.conf", "node.conf"
        }
    }

    archiveName "erp.profile"
    destinationDir(file("./build/demobench/"))
}

task extractTar<< {
    def module = arg
    def fileName = module.toString().concat("/build/distributions/").concat(module.toString()).concat("-1.0.zip")
    def zipFile = file(fileName)
    def outputDir = file("${buildDir}")
    copy {
        from zipTree(zipFile)
        into outputDir
    }
}

