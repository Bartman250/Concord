group 'com.tradeix.concord'
version '1.0'

buildscript {
    ext.kotlin_version = '1.1.60'
    ext.corda_release_group = 'net.corda'
    ext.corda_release_version = '3.1-corda'
    ext.corda_gradle_plugins_version = '3.1.0'
    ext.quasar_version = '0.7.9'
    ext.junit_version = '4.12'
    ext.gson_version = '2.8.2'
    ext.rabbit_version = '5.0.0'
    ext.spring_boot_version = '1.5.4.RELEASE'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:cordformation:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
    }
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
}

apply plugin: 'kotlin'
apply plugin: "kotlin-spring"
apply plugin: 'org.springframework.boot'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'


sourceSets {
    main.kotlin.srcDirs = []
    main.java.srcDirs = []
    main.resources.srcDirs = []
    test.kotlin.srcDirs = []
    test.java.srcDirs = []
    test.resources.srcDirs = []
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    //compile 'org.springframework.boot:spring-boot-starter-web'
    //compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // Corda integration dependencies
    cordaCompile "$corda_release_group:corda-core:$corda_release_version"
    cordaCompile "$corda_release_group:corda-finance:$corda_release_version"
    cordaCompile "$corda_release_group:corda-jackson:$corda_release_version"
    cordaCompile "$corda_release_group:corda-rpc:$corda_release_version"
    cordaCompile "$corda_release_group:corda-webserver-impl:$corda_release_version"
    cordaRuntime "$corda_release_group:corda:$corda_release_version"
    cordaRuntime "$corda_release_group:corda-webserver:$corda_release_version"

    testCompile "$corda_release_group:corda-node-driver:$corda_release_version"

    cordapp project(":tradeix-concord-shared")
    cordapp project(":tradeix-concord-shared-domain")
    cordapp project(":tradeix-concord-shared-client")
    cordapp project(":tradeix-concord-shared-cordapp")

    cordapp project(":tradeix-concord-cordapp-supplier")
    cordapp project(":tradeix-concord-cordapp-supplier-client-receiver")
    cordapp project(":tradeix-concord-cordapp-supplier-client-responder")

    cordapp project(":tradeix-concord-cordapp-funder")
    cordapp project(":tradeix-concord-cordapp-funder-client-receiver")
    cordapp project(":tradeix-concord-cordapp-funder-client-responder")
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        languageVersion = "1.1"
        apiVersion = "1.1"
        jvmTarget = "1.8"
        javaParameters = true
    }
}

def $supplierCorDapp = [
        "$project.group:tradeix-concord-shared:$project.version",
        "$project.group:tradeix-concord-shared-domain:$project.version",
        "$project.group:tradeix-concord-shared-cordapp:$project.version",
        "$project.group:tradeix-concord-shared-client:$project.version",
        "$project.group:tradeix-concord-cordapp-supplier:$project.version",
        "$project.group:tradeix-concord-cordapp-supplier-client-receiver:$project.version",
        "$project.group:tradeix-concord-cordapp-supplier-client-responder:$project.version"
]

def $funderCorDapp = [
        "$project.group:tradeix-concord-shared:$project.version",
        "$project.group:tradeix-concord-shared-domain:$project.version",
        "$project.group:tradeix-concord-shared-cordapp:$project.version",
        "$project.group:tradeix-concord-shared-client:$project.version",
        "$project.group:tradeix-concord-cordapp-funder:$project.version",
        "$project.group:tradeix-concord-cordapp-funder-client-receiver:$project.version",
        "$project.group:tradeix-concord-cordapp-funder-client-responder:$project.version"
]

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    directory "./build/nodes"
    node {
        name "O=Notary,L=London,C=GB"
        notary = [validating : true]
        p2pPort 10002
    }
    node {
        name "O=Test Supplier 1,L=London,C=GB"
        p2pPort 10005
        rpcSettings {
            address("localhost:10006")
            adminAddress("localhost:10046")
        }
        webPort 10007
        cordapps = $supplierCorDapp
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
    node {
        name "O=Test Funder 1,L=New York,C=US"
        p2pPort 10008
        rpcSettings {
            address("localhost:10009")
            adminAddress("localhost:10049")
        }
        webPort 10010
        cordapps = $funderCorDapp
        rpcUsers = [[ user: "user1", "password": "test", "permissions": ["ALL"]]]
    }
}